plugins {
    id 'java-library'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
    id "signing"
}

group = 'org.aybgim'
version = '1.0.6'

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    // The actual library depends on junit 5 API
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.3'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.3'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.3'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.aybgim'
            artifactId = 'fileassert'
            version = '1.0.6'

            from components.java

            pom {
                name = 'fileassert'
                description = 'Library for testing serialization formats against resource files, and maintaining these files with no extra code.'
                url = 'https://gitlab.com/aybgim/fileassert'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'aybgim'
                        name = 'Albert Gevorgyan'
                        email = 'john.doe@example.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@gitlab.com:aybgim/fileassert.git'
                    developerConnection = 'scm:git:ssh@gitlab.com:aybgim/fileassert.git'
                    url = 'https://gitlab.com/aybgim/fileassert.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/aybgim/fileassert")
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
        maven {
            name "GitLab"
            url "https://gitlab.com/api/v4/projects/47505299/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = 'Private-Token'
                value = gitLabPrivateToken
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = project.findProperty("sonatype.user")
            password = project.findProperty("sonatype.user")
        }
    }
}

signing {
    def signingKey = providers.environmentVariable("GPG_SIGNING_KEY")
    def signingPassphrase = providers.environmentVariable("GPG_SIGNING_PASSPHRASE")
    useInMemoryPgpKeys(signingKey.get(), signingPassphrase.get())
    sign configurations.archives
}