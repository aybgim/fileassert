plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'io.github.aybgim'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        url "https://gitlab.example.com/api/v4/groups/io.github.aybgim/-/packages/maven"
        name "GitLab"
        credentials(HttpHeaderCredentials) {
            name = 'aybgim'
            value = gitLabPrivateToken
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }
}

dependencies {
    // The actual library depends on junit 5
    implementation platform('org.junit:junit-bom:5.9.1')
    implementation 'org.junit.jupiter:junit-jupiter'

    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        myLib(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "https://gitlab.com/aybgim/api/v4/projects/fileassert/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "aybgim"
                value = gitLabPrivateToken // the variable resides in $GRADLE_USER_HOME/gradle.properties
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}